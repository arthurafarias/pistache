cmake_minimum_required (VERSION 3.1.3)
project (pistache)

INCLUDE(Dart)

set(CMAKE_CXX_FLAGS  "${CMAKE_CXX_FLAGS} -Wall -pedantic -Wextra -Wno-missing-field-initializers")
set(CMAKE_MODULE_PATH ${CMAKE_MODULE_PATH} ${PROJECT_SOURCE_DIR}/CMakeModules)

if(NOT CMAKE_BUILD_TYPE)
    set(CMAKE_BUILD_TYPE Release)
endif()

if(CMAKE_BUILD_TYPE MATCHES "Debug")
    set(CMAKE_CXX_FLAGS  "${CMAKE_CXX_FLAGS} -g -O0 -fprofile-arcs -ftest-coverage")
endif()

option(PISTACHE_BUILD_TESTS "build tests alongside the project" OFF)
option(PISTACHE_BUILD_EXAMPLES "build examples alongside the project" OFF)
option(PISTACHE_BUILD_DOCS "build docs alongside the project" OFF)
option(PISTACHE_INSTALL "add pistache as install target (recommended)" ON)
option(PISTACHE_SSL "add support for SSL server" OFF)
option(PISTACHE_CREATE_OSS_CONTRIB_TARGETS "Create custom target format code to OSS contrib" OFF)

if(CMAKE_BUILD_TYPE MATCHES "Debug")
    set(PISTACHE_CREATE_OSS_CONTRIB_TARGETS ON)
    set(PISTACHE_BUILD_TESTS ON)
    set(PISTACHE_BUILD_DOCS ON)
endif()

if (PISTACHE_CREATE_OSS_CONTRIB_TARGETS)

    find_program(CMAKE_CXX_CLANG_FORMAT NAMES clang-format)
    if (CMAKE_CXX_CLANG_FORMAT)
        message("-- Cppcheck found at " ${CMAKE_CXX_CLANG_FORMAT})
        FILE (GLOB_RECURSE SOURCES_SRC ${PROJECT_SOURCE_DIR}/src/*.cc)
        FILE (GLOB_RECURSE SOURCES_EXAMPLES ${PROJECT_SOURCE_DIR}/examples/*.cc)
        FILE (GLOB_RECURSE SOURCES_TESTS ${PROJECT_SOURCE_DIR}/tests/*.cc)
        FILE (GLOB_RECURSE SOURCES_HEADERS ${PROJECT_SOURCE_DIR}/include/*.h)
        set(FORMAT_TARGETS ${SOURCES_SRC} ${SOURCES_EXAMPLES} ${SOURCES_TESTS} ${SOURCES_HEADERS})
        add_custom_target(format COMMAND ${CMAKE_CXX_CLANG_FORMAT} -i -style=Mozilla ${FORMAT_TARGETS})
    else()
        message(FATAL_ERROR "-- Cppcheck not found")
    endif()

    find_program(CMAKE_CXX_CPPCHECK NAMES cppcheck)

    if (CMAKE_CXX_CPPCHECK)
        message("-- cppcheck found at " ${CMAKE_CXX_CPPCHECK})
        list(
            APPEND CMAKE_CXX_CPPCHECK 
                "--enable=all"
                "--suppress=*:${PROJECT_SOURCE_DIR}/third-party*"
                "--suppress=*:${PROJECT_SOURCE_DIR}/tests*"
        )
    else()
        message(FATAL_ERROR "-- cppcheck not found")
        set(CMAKE_CXX_CPPCHECK "")
    endif()

    find_program(CTEST_COVERAGE_COMMAND NAMES gcov)

    if (CTEST_COVERAGE_COMMAND)
        message("-- gcov found at " ${CTEST_COVERAGE_COMMAND})
        add_custom_target(coverage COMMAND ${CMAKE_CTEST_COMMAND}  --force-new-ctest-process --test-action coverage)
    else()
        message(FATAL_ERROR "-- gcov not found")
        set(CTEST_COVERAGE_COMMAND "")
    endif()

    find_program(CTEST_MEMORYCHECK_COMMAND NAMES valgrind)

    if (CTEST_MEMORYCHECK_COMMAND)
        message("-- valgrind found at " ${CTEST_MEMORYCHECK_COMMAND})
        add_custom_target(memcheck COMMAND ${CMAKE_CTEST_COMMAND}  --force-new-ctest-process --test-action memcheck)
    else()
        message(FATAL_ERROR "-- valgrind not found")
        set(CTEST_MEMORYCHECK_COMMAND "")
    endif()

endif()

# CMAKE version less than 3.8 does not understand the CMAKE_CXX_FLAGS
# set to 17, so a manual check if performed on older version
if(${CMAKE_VERSION} VERSION_LESS "3.8.0")
    include(CheckCXXCompilerFlag)
    CHECK_CXX_COMPILER_FLAG("-std=c++17" COMPILER_SUPPORTS_CXX17)
    if(COMPILER_SUPPORTS_CXX17)
        set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -std=c++17")
    else()
        set(CMAKE_CXX_STANDARD 14)
    endif()
else()
    # Clang exports C++17 in std::experimental namespace (tested on Clang 5 and 6).
    # This gives an error on date.h external library.
    # Following workaround forces Clang to compile at best with C++14
    if(CMAKE_CXX_COMPILER_ID STREQUAL "Clang")
        set(CMAKE_CXX_STANDARD 14)
    elseif (CMAKE_COMPILER_IS_GNUCC)
        if(CMAKE_CXX_COMPILER_VERSION VERSION_GREATER 7.0)
            set(CMAKE_CXX_STANDARD 17)
        else()
            set(CMAKE_CXX_STANDARD 14)
        endif()
    else()
        set(CMAKE_CXX_STANDARD 17)
    endif()
endif()

set(CMAKE_CXX_STANDARD_REQUIRED ON)
set(CMAKE_CXX_EXTENSIONS OFF)

if (PISTACHE_SSL)
    add_definitions(-DPISTACHE_USE_SSL)
    link_libraries(-lssl -lcrypto)
endif (PISTACHE_SSL)

include_directories (${CMAKE_CURRENT_SOURCE_DIR}/include)
# Set version...

    # Major and minor version...
    set(VERSION_MAJOR 0)
    set(VERSION_MINOR 0)

    # Make available in a header file...
    configure_file (
        "include/pistache/version.h.in"
        "include/pistache/version.h"
        @ONLY
    )

# Set libraries...

    # Minimum...
    set(LIBS "-lpistache -lpthread")

    # If building with OpenSSL support...
    if(PISTACHE_SSL)
        set(LIBS "${LIBS} -lssl -lcrypto")
    endif(PISTACHE_SSL)

if(PISTACHE_INSTALL)
    # Install header...
    install (
        FILES
        ${CMAKE_BINARY_DIR}/include/pistache/version.h
        DESTINATION
        include/pistache/
    )
endif()

# Configure the pkg-config metadata...

    # Initialize the metadata variables and to support remote builds...
    set(prefix         ${CMAKE_INSTALL_PREFIX})
    set(exec_prefix    ${CMAKE_INSTALL_PREFIX}/bin)
    set(libdir         ${CMAKE_INSTALL_PREFIX}/lib)
    set(libs           ${LIBS})
    set(includedir     ${CMAKE_INSTALL_PREFIX}/include)
    set(version        ${VERSION_MAJOR}.${VERSION_MINOR})

    # Perform substitutions...
    configure_file (
        "libpistache.pc.in"
        "libpistache.pc"
        @ONLY
    )

if(PISTACHE_INSTALL)
    # Install pkg-config metadata into standard location within the prefix...
    install (
        FILES
        ${CMAKE_BINARY_DIR}/libpistache.pc
        DESTINATION
        lib/pkgconfig/
    )
endif()

add_subdirectory (src)

if (PISTACHE_BUILD_EXAMPLES)
    add_subdirectory (examples)
endif()

if (PISTACHE_BUILD_TESTS)
    find_package(GTest)
    if (GTEST_FOUND)
        include_directories(${GTEST_INCLUDE_DIRS})
    else()
        ADD_SUBDIRECTORY (third-party/googletest)
        include_directories(${gtest_SOURCE_DIR}/include ${gtest_SOURCE_DIR})
    endif()

    enable_testing()
    add_subdirectory(tests)
endif()

if (PISTACHE_BUILD_DOCS)

    find_package(Doxygen
            REQUIRED dot
            OPTIONAL_COMPONENTS mscgen dia)

    if (DOXYGEN_FOUND)

        set(DOXYGEN_IN ${CMAKE_SOURCE_DIR}/docs/Doxyfile.in)
        set(DOXYGEN_OUT ${CMAKE_CURRENT_BINARY_DIR}/docs/Doxyfile)

        configure_file(${DOXYGEN_IN} ${DOXYGEN_OUT} @ONLY)

        add_custom_target(docs
            COMMAND ${DOXYGEN_EXECUTABLE} ${DOXYGEN_OUT}
            WORKING_DIRECTORY ${CMAKE_CURRENT_BINARY_DIR}/docs
            COMMENT "Generating doxygen documentation"
            VERBATIM)

        message("-- Adding doxygen documentation done")

    else(DOXYGEN_FOUND)
        message(FATAL_ERROR "Doxygen need to be installed to generate the doxygen documentation")
    endif(DOXYGEN_FOUND)

endif()